import org.apache.tools.ant.filters.*

dependencies {
    compile 'ch.qos.logback:logback-classic:1.2.1'
    compile 'com.amazonaws:aws-java-sdk-ec2:1.11.140'
    compile 'com.amazonaws:aws-java-sdk-route53:1.11.140'
    compile 'com.jcraft:jsch:0.1.54'
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'org.apache.bval:bval-jsr:1.1.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'org.fusesource.jansi:jansi:1.14'
    compile 'org.apache.ivy:ivy:2.4.0'
}

processResources {
    filter ReplaceTokens, tokens: [
            "application.version" : version,
            "build.date"          : new Date().format("dd-MM-yyyy HH:mm"),
            "build.revision"      : "git rev-parse HEAD".execute().text 
    ]
}

test {
    maxParallelForks = project.hasProperty('testAws') ? 1 : 4
    
    useJUnit {
        if (project.hasProperty('testAws')) { // activate AWS tests
            systemProperty "awsAccessKeyId",     project.getProperty("awsAccessKeyId")
            systemProperty "awsAccessSecretKey", project.getProperty("awsAccessSecretKey")
            systemProperty "awsRegion",          project.getProperty("awsRegion")
        } else {
            excludeCategories 'io.infrastructor.core.inventory.aws.AwsCategory'
        }
    }
}